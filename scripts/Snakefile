configfile: "parameters.yaml"

rule all:
	input:
		# f"{config['OUTPUT_DIR']}/sizes.result",
		f"{config['OUTPUT_DIR']}/timings.result"
	threads:
		config["NUM_THREADS"]


rule check_output_dir:
	output:
		temp(touch('output_checked'))
	params:
		OUTPUT_DIR = config["OUTPUT_DIR"]
	shell:
		"""
		if ! [[ -d {params.OUTPUT_DIR} ]]; then
			echo "Directory {params.OUTPUT_DIR} does not exist. Exiting."
		fi
		touch {output}
		"""


rule check_raptor_binary:
	output:
		temp(touch('raptor_binary_checked'))
	params:
		RAPTOR_BINARY = config["RAPTOR_BINARY"]
	shell:
		"""
		if ! [[ -s {params.RAPTOR_BINARY} ]]; then
			echo "Executable {params.RAPTOR_BINARY} does not exist. Exiting."
			exit 1
		fi
		if ! {params.RAPTOR_BINARY} --help &> /dev/null; then
			echo "Executable {params.RAPTOR_BINARY} cannot be run. Exiting."
			exit 1
		fi
		touch {output}
		"""


rule check_input_files:
	output:
		temp(touch('input_file_checked'))
	params:
		INPUT_DIR = config["INPUT_DIR"],
		FILENAMES_FILE = config["FILENAMES_FILE"],
		READS_FILE = config["READS_FILE"],
		QUERY_ERRORS = config["DEFAULT_PARAMS"]["QUERY_ERRORS"],
		QUERY_LENGTH = config["DEFAULT_PARAMS"]["QUERY_LENGTH"]
	shell:
		"""
		if ! [[ -s {params.INPUT_DIR}/{params.FILENAMES_FILE} ]]; then
			echo "Input file {params.INPUT_DIR}/{params.FILENAMES_FILE} does not exist. Exiting."
			exit 1
		fi
		if ! [[ -s {params.INPUT_DIR}/{params.READS_FILE}/all.fastq ]]; then
			echo "Input file {params.INPUT_DIR}/{params.READS_FILE}/all.fastq does not exist. Exiting."
			exit 1
		fi
		touch {output}
		"""

rule raptor_layout:
	output:
		LAYOUT_FILE = 'build/{key}={param}/layout'
	threads:
		config["NUM_THREADS"]
	params:
		RAPTOR_BINARY = config["RAPTOR_BINARY"],
		INPUT_DIR = config["INPUT_DIR"],
		FILENAMES_FILE = config["FILENAMES_FILE"],
		LAYOUT_FILE = 'build/{key}={param}/layout',
		LAYOUT_TIME = 'build/{key}={param}/layout.time',
		MAXIMUM_FPR = config["DEFAULT_PARAMS"]["M_FPR"],
		NUM_HASHES = config["DEFAULT_PARAMS"]["NUM_HASHES"],
		KMER = config["DEFAULT_PARAMS"]["KMER_SIZE"],
		R_FPR = config["DEFAULT_PARAMS"]["R_FPR"],
		ALPHA = config["DEFAULT_PARAMS"]["ALPHA"],
		T_MAX = config["DEFAULT_PARAMS"]["T_MAX"],
	shell:
		"""
		echo "[$(date +"%Y-%m-%d %T")] Running raptor layout {wildcards.key}={wildcards.param}."
		# Raptor layout doesn't provide `--timing-output`, so we use /usr/bin/time with a custom format
		kmer=$([[ {wildcards.key} == "kmer" ]] && echo {wildcards.param} || echo {params.KMER})
		hash=$([[ {wildcards.key} == "hash" ]] && echo {wildcards.param} || echo {params.NUM_HASHES})
		relaxed_fpr=$([[ {wildcards.key} == "r-relaxed" ]] && echo {wildcards.param} | sed 's/_/./g' || echo {params.R_FPR})
		alpha=$([[ {wildcards.key} == "alpha" ]] && echo {wildcards.param} | sed 's/_/./g' || echo {params.ALPHA})
		tmax=$([[ {wildcards.key} == "tmax" ]] && echo {wildcards.param} || echo {params.T_MAX})
		/usr/bin/time -o {params.LAYOUT_TIME} -f "wall_clock_time_in_seconds\tpeak_memory_usage_in_kibibytes\n%e\t%M" \
		{params.RAPTOR_BINARY} layout\
			--input {params.INPUT_DIR}/{params.FILENAMES_FILE} \
			--output {params.LAYOUT_FILE} \
			--fpr {params.MAXIMUM_FPR} \
			--disable-estimate-union \
			--disable-rearrangement \
			--relaxed-fpr $relaxed_fpr \
			--kmer $kmer \
			--hash $hash \
			--alpha $alpha \
			--tmax $tmax \
			--threads {threads}
		"""


rule raptor_build:
	input:
		'build/{key}={param}/layout'
	output:
		'build/{key}={param}/index'
	threads:
		config["NUM_THREADS"]
	params:
		RAPTOR_BINARY = config["RAPTOR_BINARY"],
		LAYOUT_FILE = 'build/{key}={param}/layout',
		INDEX_FILE = 'build/{key}={param}/index',
		INDEX_TIME = 'build/{key}={param}/index.time',
		WINDOW_SIZE = config["DEFAULT_PARAMS"]["WINDOW_SIZE"],
	shell:
		"""
		echo "[$(date +"%Y-%m-%d %T")] Running raptor build {wildcards.key}={wildcards.param}."
		{params.RAPTOR_BINARY} build \
			--input {params.LAYOUT_FILE} \
			--output {params.INDEX_FILE} \
			--window {params.WINDOW_SIZE} \
			--quiet \
			--timing-output {params.INDEX_TIME} \
			--threads {threads}
		"""


rule raptor_search:
	input:
		"raptor_binary_checked",
		"output_checked",
		"input_file_checked",
		'build/{key}={param}/index'
	output:
		'build/{key}={param}/out'
	threads:
		config["NUM_THREADS"]
	params:
		RAPTOR_BINARY = config["RAPTOR_BINARY"],
		INPUT_DIR = config["INPUT_DIR"],
		INDEX_FILE = 'build/{key}={param}/index',
		RESULT_FILE = 'build/{key}={param}/out',
		RESULT_TIME = 'build/{key}={param}/out.time',
		QUERY_LENGTH = config["DEFAULT_PARAMS"]["QUERY_LENGTH"],
		QUERY_ERRORS = config["DEFAULT_PARAMS"]["QUERY_ERRORS"],
	shell:
		"""
		echo "[$(date +"%Y-%m-%d %T")] Running raptor search {wildcards.key}={wildcards.param}."
		{params.RAPTOR_BINARY} search \
			--index {params.INDEX_FILE} \
			--query {params.INPUT_DIR}/reads_e{params.QUERY_ERRORS}_{params.QUERY_LENGTH}/all.fastq \
			--output {params.RESULT_FILE} \
			--error {params.QUERY_ERRORS} \
			--query_length {params.QUERY_LENGTH} \
			--quiet \
			--timing-output {params.RESULT_TIME} \
			--threads {threads}
		"""


rule store_timings:
	input:
		expand('build/{param}/out',
			param = [f"alpha={str(param).replace('.', '_')}" for param in config["PARAMS"]["ALPHA"]] +
					[f"tmax={param}" for param in config["PARAMS"]["T_MAX"]] +
					[f"hash={param}" for param in config["PARAMS"]["NUM_HASHES"]] +
					[f"kmer={param}" for param in config["PARAMS"]["KMER_SIZE"]] +
					[f"relaxed-fpr={str(param).replace('.', '_')}" for param in config["PARAMS"]["RELAXED_FPR"]])
	output:
		temp(touch(f"{config['OUTPUT_DIR']}/timings.result"))
	threads:
		config["NUM_THREADS"]
	shell:
		"""
		echo "[$(date +"%Y-%m-%d %T")] Storing timings."
		# python summerize_timings {input} > {output}
		"""

